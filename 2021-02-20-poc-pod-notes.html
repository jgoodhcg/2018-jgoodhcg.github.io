<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml" lang="en" xml:lang="en">
<head>
    <meta charset="utf-8"/>
    <title>JGood: üóíÔ∏è A POC for taking Roam notes on audio</title>
    
<meta name="keywords" content="podcast,tick,react-native,clojure,cljs,clj,vega,oz,reagent,roam,expo,vega-lite,time,shadow-cljs,create-kit,video-production,notes,npm,react-native-gesture-handler,memento mori">

<meta name="description" content="My podcast player used to include the current position with the export from the share feature. When it was removed, for whatever reason, it kind of ruined my note process. I like to take quick notes with the position so I can revise by listening back whenever I get around to it. Now I have to include the position manually when I export to Roam&#39;s quick capture screen. It&#39;s an annoying context switch.This problem got me thinking about taking notes on audio in general . There isn&#39;t a good solution for the niche of second brain users. I want Roam and Notion integrated directly in the player for seamless note taking without context switching. I actually have the skills needed to build this, so I made a Proof of Concept over a weekend.">

<meta property="og:description" content="My podcast player used to include the current position with the export from the share feature. When it was removed, for whatever reason, it kind of ruined my note process. I like to take quick notes with the position so I can revise by listening back whenever I get around to it. Now I have to include the position manually when I export to Roam&#39;s quick capture screen. It&#39;s an annoying context switch.This problem got me thinking about taking notes on audio in general . There isn&#39;t a good solution for the niche of second brain users. I want Roam and Notion integrated directly in the player for seamless note taking without context switching. I actually have the skills needed to build this, so I made a Proof of Concept over a weekend.">

<meta property="og:url" content="https://jgood.io/2021-02-20-poc-pod-notes" />
<meta property="og:title" content="üóíÔ∏è A POC for taking Roam notes on audio" />
<meta property="og:type" content="article" />

    <link rel="canonical" href="https://jgood.io/2021-02-20-poc-pod-notes">
    <meta name="viewport" content="width=device-width, initial-scale=1">
    <link href="//fonts.googleapis.com/css?family=Alegreya:400italic,700italic,400,700" rel="stylesheet"
          type="text/css">
    <link rel="stylesheet" href="//maxcdn.bootstrapcdn.com/bootstrap/3.3.0/css/bootstrap.min.css">
    <link href="//maxcdn.bootstrapcdn.com/font-awesome/4.2.0/css/font-awesome.min.css" rel="stylesheet">
    <link rel="stylesheet" href="//cdnjs.cloudflare.com/ajax/libs/highlight.js/9.7.0/styles/default.min.css">
    <link href="/css/screen.css" rel="stylesheet" type="text/css" />
    <!-- Global site tag (gtag.js) - Google Analytics -->
    <script async src="https://www.googletagmanager.com/gtag/js?id=UA-74205906-2"></script>
    <script>
        window.dataLayer = window.dataLayer || [];
        function gtag(){dataLayer.push(arguments);}
        gtag('js', new Date());

        gtag('config', 'UA-74205906-2');
    </script>
</head>
<body>


<nav class="navbar navbar-default">
    <div class="container">
        <div class="navbar-header">
            <button type="button" class="navbar-toggle collapsed" data-toggle="collapse" data-target="#navbar" aria-expanded="false" aria-controls="navbar">
                <span class="sr-only">Toggle navigation</span>
                <span class="icon-bar"></span>
                <span class="icon-bar"></span>
                <span class="icon-bar"></span>
            </button>
            <a class="navbar-brand" href="/">JGood</a>
        </div>
        <div id="navbar" class="navbar-collapse collapse">
            <ul class="nav navbar-nav navbar-right">
                <li ><a href="/">Home</a></li>
                <li
                ><a href="/archives">Archives</a></li>
                
                <li
                >
                  <a href="/resume">Resume</a>
                </li>
                
                <li><a href="/feed.xml">RSS</a></li>
            </ul>
        </div><!--/.nav-collapse -->
    </div><!--/.container-fluid -->
</nav>


<div class="container">


    <div class="row">
        <div class="col-lg-9">
            <div id="content">
                
<div id="post">
    <div class="post-header">
    <div id="post-meta" class="row">
        <div class="col-lg-6">February 20, 2021</div>
        
    </div>
    <h2>üóíÔ∏è A POC for taking Roam notes on audio</h2>
</div>
<div>
    
    <p>My podcast player used to include the current position with the export from the share feature. When it was removed, for whatever reason, it kind of ruined my note process. I like to take quick notes with the position so I can revise by listening back whenever I get around to it. Now I have to include the position manually when I export to Roam's quick capture screen. It's an annoying context switch.</p><p>This problem got me thinking about taking notes on audio in general . There isn't a <em>good</em> solution for the niche of <em>second brain</em> users. I want Roam and Notion integrated directly in the player for seamless note taking without context switching. I actually have the skills needed to build this, so I made a Proof of Concept over a weekend.</p><p>The next step is getting a mobile app in the play and app store.<br /><a href="https://airtable.com/shrpcaRLwMAixJteh">Get on the wait list</a></p><h2>Vision</h2><h3>Version 1.0</h3><ul><li>‚èØÔ∏è Basic management and playing podcast feeds</li><li>üóíÔ∏è Take a text note on an episode tied to a position</li><li>üß† Roam page auto-complete in note editor</li><li>üì§ Notes can export individually and get marked when they do</li></ul><h3>Long term</h3><ul><li>Audiobooks</li><li>Other app integration -- <strong>notion</strong></li><li>Profiles and cloud backups</li><li>Synced web app</li><li>Block level syncing with roam</li></ul><h2>POC</h2><ul><li>I've got playback, note taking, and auto-complete working. I also know how to go about integrating page titles. More on that further down.</li></ul><figure class="video_container"><video height="800" controls="true" allowfullscreen="true" poster="./../../img/2021-02-20-poc-pod-notes/poc-poster.png"><source src="https://files.jgood.tech/project-din-demo-001.mp4" type="video/mp4"></source></video></figure><h3>How v-1.0 will get your Roam page titles</h3><p><a href="https://github.com/artpi/roam-research-private-api">This unofficial api project</a> paired with AWS Lambda can actually create a secure and simple process for a mobile app to get access to your graph's page titles.</p><p><img src="./../../img/2021-02-20-poc-pod-notes/v1-flow-sequence-diagram.png" alt="img" title="Starting expo and shadow-cljs" /></p><h3>Source code</h3><p>If you are into it, here is 99% of the POC source code. Fun fact, it's written in <em>Clojurescript</em>, the same language Roam is written in!</p><pre><code class="language-clojure">(ns app.index
  (:require
   ["expo" :as ex]
   ["expo-constants" :as expo-constants]
   ["expo-av" :as av]
   ["react-native" :as rn]
   ["react" :as react]
   ["@react-navigation/native" :as nav]
   ["@react-navigation/stack" :as rn-stack]
   ["react-native-paper" :as paper]
   ["react-native-rss-parser" :as rss]
   ["react-native-controlled-mentions" :as cm]
   ["tailwind-rn" :default tailwind-rn]

   [applied-science.js-interop :as j]
   [camel-snake-kebab.core :as csk]
   [camel-snake-kebab.extras :as cske]
   [cljs.core.async :refer [go]]
   [cljs.core.async.interop :refer [&lt;p!]]
   [reagent.core :as r]
   [re-frame.core :refer [subscribe dispatch dispatch-sync]]
   [shadow.expo :as expo]
   [lambdaisland.fetch :as fetch]

   [app.fx]
   [app.handlers]
   [app.subscriptions]
   [app.helpers :refer [&lt;sub &gt;evt]]))

(defn tw [style-str] (-&gt; style-str
                         tailwind-rn
                         (js-&gt;clj :keywordize-keys true)))

(def value (r/atom ""))

(def pages ["podcast" "indie-hackers" "Courtland Allen" "marketing" "npm" "project/din"])

(defonce podcast-atom (r/atom nil))

(defonce progress-atom (r/atom
                         {:width    "0%"
                          :duration nil
                          :position nil}))

(defonce playing-atom (r/atom :stopped))

(defonce playback-atom (r/atom nil))

(defonce notes-atom (r/atom {:notes    []
                             :selected nil}))

(defn pad [n] (if (-&gt; n str count (&lt; 2))
                (str "0" n)
                (str n)))

(defn millis-&gt;str [millis]
  ;; TODO add padding
  ;; https://stackoverflow.com/a/9763769/5040125
  (let [ms   (rem millis 1000)
        left (-&gt; millis (- ms) (/ 1000))
        sec  (-&gt; left (rem 60))
        left (-&gt; left (- sec) (/ 60))
        min  (-&gt; left (rem 60))
        hr   (-&gt; left (- min) (/ 60))]
    (str (pad hr) ":" (pad min) ":" (pad sec))))

(defn page-suggestions [e]
  (let [on-suggest  (-&gt; e (j/get :onSuggestionPress))
        maybe-page  (-&gt; e (j/get :keyword))
        suggestions (when (-&gt; maybe-page count (&gt; 0))
                      (-&gt;&gt; pages
                           (filter #(re-find (re-pattern (str "(?i)" maybe-page)) %))))]
    (when (some? maybe-page)
      (r/as-element
        [(fn []
           [:&gt; rn/View
            (for [title suggestions]
              [:&gt; paper/List.Item
               {:key      (str (random-uuid))
                :title    title
                :on-press #(on-suggest #js {:id (str (random-uuid)) :name title})}])])]))))

(defn get-notes []
  (let [notes @notes-atom]
    {:notes          (-&gt; notes :notes)
     :selected-index (-&gt; notes :selected)
     :selected-note  (-&gt; (-&gt; notes :notes)
                         (get (-&gt; notes :selected) nil))} ))

(defn my-text-input []
  (let [{:keys [selected-index selected-note]} (get-notes)]
    [:&gt; cm/MentionInput
     {:style                  (tw "text-gray-50")
      :text-align             "left"
      :text-align-vertical    "top"
      :multi-line             true
      :number-of-lines        10
      :placeholder            "Make note here"
      :placeholder-text-color (:color (tw "text-gray-500"))
      :value                  (:text selected-note)
      :on-change              #(swap! notes-atom assoc-in [:notes selected-index :text] %)

      :part-types
      [{:trigger                   "#"
        :getPlainString            #(-&gt; % (j/get :name) ((fn [s] (str "[[" s "]]"))))
        :isInsertSpaceAfterMention true
        :textStyle                 (tw "text-blue-400")
        :renderSuggestions         page-suggestions}]}]))

(defn on-status-change [status]
  (j/let [^:js {:keys [isLoaded isPlaying positionMillis durationMillis]} status]
    (if isLoaded
      (reset! playing-atom (if isPlaying :playing :stopped))
      (reset! playing-atom :loading))

    (reset! progress-atom {:width    (-&gt; positionMillis
                                         (/ durationMillis)
                                         (* 100)
                                         (str "%"))
                           :position positionMillis
                           :duration durationMillis})))

(defn on-initial-play [episode-uri]
  (go (-&gt; av
          (j/get :Audio)
          (j/get :Sound)
          (j/call :createAsync
                  #js {:uri episode-uri}
                  #js {:shouldPlay true}
                  on-status-change)
          &lt;p!
          ((fn [x] (reset! playback-atom (j/get x :sound)))))))

(defn on-add-note []
  (when-some [position (-&gt; @progress-atom :position)]
    (swap! notes-atom
           #(do
              (-&gt; %
                  (assoc :selected (-&gt; % :notes count)) ;; new note isn't added yet "off by one"
                  (update-in [:notes] conj
                             {:position position
                              :left     (-&gt; position
                                            (/ (-&gt; @progress-atom :duration))
                                            (* 100)
                                            (str "%"))
                              :text     ""}))))))

(defn on-forward-30 []
  (when-some [playback @playback-atom]
    (let [{:keys [position duration]} @progress-atom]
      (-&gt; playback (j/call :setPositionAsync
                           (-&gt; position (+ 30000)
                               (min (-&gt; duration (- 30000)))))))))

(defn on-backward-30 []
  (when-some [playback @playback-atom]
    (let [{:keys [position]} @progress-atom]
      (-&gt; playback (j/call :setPositionAsync
                           (-&gt; position (- 30000)
                               (max 30000)))))))

(defn on-next-note []
  (swap! notes-atom
         (fn [na-val]
           (let [max       (count (:notes na-val))
                 new-index (min (inc (:selected na-val))
                                max)]
             (assoc-in na-val [:selected] new-index)))))

(defn on-prev-note []
  (swap! notes-atom
         (fn [na-val]
           (let [new-index (max (dec (:selected na-val))
                                0)]
             (assoc-in na-val [:selected] new-index)))))

(defn on-share []
  (let [{:keys [selected-note]} (get-notes)
        position                (:position selected-note)
        position-str            (-&gt; position millis-&gt;str)
        text                    (-&gt; selected-note
                                    :text
                                    (cm/replaceMentionValues
                                      (fn [mt] (str "[[" (j/get mt :name) "]]"))))
        podcast                 @podcast-atom
        title                   (-&gt; podcast (j/get :title))
        episode                 (-&gt; podcast (j/get :items) first)
        episode-title           (-&gt; episode (j/get :title))]
    (-&gt; rn/Share
        (j/call :share #js {:message (str "Note from " title " \n "
                                          "episode: " episode-title " \n "
                                          "position: " position-str " \n "
                                          text " \n "
                                          "https://cool-podcast-notes.io/" (random-uuid) "/p=" position)}))))

(defn screen1 [props]
  (r/as-element
    [(fn []
       (let [podcast                 @podcast-atom
             playing                 @playing-atom
             progress                @progress-atom
             progress-width          (:width progress)
             position-str            (-&gt; progress :position millis-&gt;str)
             duration-str            (-&gt; progress :duration millis-&gt;str)
             image-uri               (-&gt; podcast (j/get :image) (j/get :url))
             title                   (-&gt; podcast (j/get :title))
             episode                 (-&gt; podcast (j/get :items) first)
             episode-title           (-&gt; episode (j/get :title))
             episode-uri             (-&gt; episode (j/get :enclosures) first (j/get :url))
             on-initial-play         #(on-initial-play episode-uri)
             playback                @playback-atom
             on-play                 #(-&gt; playback (j/call :playAsync))
             on-pause                #(-&gt; playback (j/call :pauseAsync))
             {:keys [notes
                     selected-note]} (get-notes)]

         [:&gt; rn/SafeAreaView {:style (tw "flex flex-1")}
          [:&gt; rn/StatusBar {:visibility "hidden"}]
          [:&gt; paper/Surface {:style (tw "flex flex-1 bg-gray-900")}
           [:&gt; rn/View {:style (tw "flex flex-1 justify-start")}
            ;; podcast info
            [:&gt; rn/View {:style (tw "flex flex-row flex-nowrap p-2")}
             ;; image
             [:&gt; paper/Card.Cover {:source {:uri image-uri}
                                   :style  (tw "h-28 w-28")}]

             ;; title
             [:&gt; rn/View {:style (tw "flex flex-col ml-2 mr-32")}
              [:&gt; paper/Title title]
              [:&gt; paper/Text episode-title]]]

            ;; progress bar and notes
            [:&gt; rn/View {:style (tw "mt-2 px-2 h-80")}
             [:&gt; rn/View {:style (tw "h-full w-full")}
              ;; progress bar
              [:&gt; rn/View {:style (tw "absolute left-0 w-full h-4 bg-purple-400 opacity-50 rounded") }]
              [:&gt; rn/View {:style (merge {:width progress-width} (tw "absolute left-0 h-4 bg-purple-400 rounded-l"))}]
              [:&gt; rn/View {:style (tw "absolute right-0 top-4")} [:&gt; paper/Text {:style (tw "text-gray-400")} duration-str]]
              [:&gt; rn/View {:style (tw "absolute left-0 top-4")} [:&gt; paper/Text {:style (tw "text-gray-400")} position-str]]

              ;; notes
              (for [{:keys [left]} notes]
                [:&gt; rn/View {:key   (random-uuid)
                             :style (merge {:left left} (tw "absolute w-1 h-4 bg-gray-200 "))}])

              ;; selected note
              (when-some [{:keys [left]} selected-note]
                [:&gt; rn/View {:style (merge {:left left} (tw "absolute -top-1 w-1 h-12 bg-yellow-400 rounded-t"))}])

              (when (some? selected-note)
                [:&gt; rn/View {:style (tw "absolute left-0 top-11 w-full h-64 bg-gray-700 border-4 border-yellow-400 rounded")}
                 [:&gt; rn/View {:style (tw "p-4")}
                  [my-text-input]]])

              (when (some? selected-note)
                [:&gt; rn/View {:style (tw "absolute right-2 bottom-6")}
                 [:&gt; paper/IconButton {:icon "share" :on-press on-share :size 24}]])]]


            ;; controlls
            [:&gt; rn/View {:style (tw "flex flex-row justify-between items-center px-4 h-32")}
             [:&gt; paper/IconButton {:icon "arrow-left" :on-press on-prev-note}]
             [:&gt; paper/IconButton {:icon "rewind" :disabled true}]
             [:&gt; paper/IconButton {:icon "rewind-30" :on-press on-backward-30}]

             (case playing
               :stopped [:&gt; paper/IconButton {:icon "play" :size 42 :on-press (if (some? playback) on-play on-initial-play)}]
               :playing [:&gt; paper/IconButton {:icon "pause" :size 42 :on-press on-pause}]
               :loading [:&gt; paper/ActivityIndicator {:animating true :size 42}])

             [:&gt; paper/IconButton {:icon "fast-forward-30" :on-press on-forward-30}]
             [:&gt; paper/IconButton {:icon "fast-forward" :disabled true}]
             [:&gt; paper/IconButton {:icon "arrow-right" :on-press on-next-note}]]

            ;; add note
            [:&gt; rn/View {:style (tw "flex flex-row justify-end mt-4 p-2")}
             [:&gt; paper/Button {:mode "contained" :icon "note" :on-press on-add-note} "Add note"]]]]]))]))

(def stack (rn-stack/createStackNavigator))

(defn navigator [] (-&gt; stack (j/get :Navigator)))

(defn screen [props] [:&gt; (-&gt; stack (j/get :Screen)) props])

(defn root []
  (let [theme           (&lt;sub [:theme])
        !route-name-ref (clojure.core/atom {})
        !navigation-ref (clojure.core/atom {})]

    [:&gt; paper/Provider
     {:theme (case theme
               :light paper/DefaultTheme
               :dark  paper/DarkTheme
               paper/DarkTheme)}

     [:&gt; nav/NavigationContainer
      {:ref             (fn [el] (reset! !navigation-ref el))
       :on-ready        (fn []
                          (swap! !route-name-ref merge {:current (-&gt; @!navigation-ref
                                                                     (j/call :getCurrentRoute)
                                                                     (j/get :name))}))
       :on-state-change (fn []
                          (let [prev-route-name    (-&gt; @!route-name-ref :current)
                                current-route-name (-&gt; @!navigation-ref
                                                       (j/call :getCurrentRoute)
                                                       (j/get :name))]
                            (when (not= prev-route-name current-route-name)
                              ;; This is where you can do side effecty things like analytics
                              (&gt;evt [:some-fx-example (str "New screen encountered " current-route-name)]))
                            (swap! !route-name-ref merge {:current current-route-name})))}

      [:&gt; (navigator) {:header-mode "none"}
       (screen {:name      "Screen1"
                :component (paper/withTheme screen1)})]]]))

(defn start
  {:dev/after-load true}
  []
  (go (-&gt; "https://feeds.transistor.fm/the-indie-hackers-podcast"
          (fetch/get {:mode "no-cors"})
          &lt;p!
          :body
          (-&gt;&gt; (j/call rss :parse))
          &lt;p!
          (-&gt;&gt; (reset! podcast-atom))))
  (expo/render-root (r/as-element [root])))

(defn init []
  (dispatch-sync [:initialize-db])
  (start))
</code></pre>
</div>

<div id="post-tags">
    <b>Tags: </b>
    
    <a href="/tags/podcast">podcast</a>
    
    <a href="/tags/react-native">react-native</a>
    
    <a href="/tags/cljs">cljs</a>
    
    <a href="/tags/roam">roam</a>
    
    <a href="/tags/expo">expo</a>
    
</div>


    <div id="prev-next">
        
        
        <a class="right" href="/2020-12-20-failed-gesture-experiment">‚ùå Failed Gesture Experiment &raquo;</a>
        
    </div>

    


</div>

            </div>
        </div>

        <div class="col-md-3">
            <div id="sidebar">
                <h3>Links</h3>
                <ul id="links">
                  <li>
                    <i class="fa fa-linkedin" aria-hidden="true"></i>
                    <a href="https://www.linkedin.com/in/jgoodhcg/">Linked In</a>
                  </li>
                  <li>
                    <i class="fa fa-github" aria-hidden="true"></i>
                    <a href="https://github.com/jgoodhcg">Github</a>
                  </li>
                  <li>
                    <i class="fa fa-twitter" aria-hidden="true"></i>
                    <a href="https://twitter.com/jgoodhcg">Twitter</a>
                  </li>
                  
                </ul>
                
                <div id="recent">
                    <h3>Recent Posts</h3>
                    <ul>
                        
                        <li><a href="/2021-02-20-poc-pod-notes">üóíÔ∏è A POC for taking Roam notes on audio</a></li>
                        
                        <li><a href="/2020-12-20-failed-gesture-experiment">‚ùå Failed Gesture Experiment</a></li>
                        
                        <li><a href="/2020-12-20-video-production-notes">üìΩÔ∏è Video Production Notes</a></li>
                        
                    </ul>
                </div>
                
                
                <div id="tags">
                    <h3>Tags</h3>
                    <ul>
                        
                        <li><a href="/tags/podcast">podcast</a></li>
                        
                        <li><a href="/tags/tick">tick</a></li>
                        
                        <li><a href="/tags/react-native">react-native</a></li>
                        
                        <li><a href="/tags/clojure">clojure</a></li>
                        
                        <li><a href="/tags/cljs">cljs</a></li>
                        
                        <li><a href="/tags/clj">clj</a></li>
                        
                        <li><a href="/tags/vega">vega</a></li>
                        
                        <li><a href="/tags/oz">oz</a></li>
                        
                        <li><a href="/tags/reagent">reagent</a></li>
                        
                        <li><a href="/tags/roam">roam</a></li>
                        
                        <li><a href="/tags/expo">expo</a></li>
                        
                        <li><a href="/tags/vega-lite">vega-lite</a></li>
                        
                        <li><a href="/tags/time">time</a></li>
                        
                        <li><a href="/tags/shadow-cljs">shadow-cljs</a></li>
                        
                        <li><a href="/tags/create-kit">create-kit</a></li>
                        
                        <li><a href="/tags/video-production">video-production</a></li>
                        
                        <li><a href="/tags/notes">notes</a></li>
                        
                        <li><a href="/tags/npm">npm</a></li>
                        
                        <li><a href="/tags/react-native-gesture-handler">react-native-gesture-handler</a></li>
                        
                        <li><a href="/tags/memento mori">memento mori</a></li>
                        
                    </ul>
                </div>
                
            </div>
        </div>
    </div>
    <footer>Copyright &copy; 2021 Justin Good
        <p style="text-align: center;">Powered by <a href="http://cryogenweb.org">Cryogen</a></p></footer>
</div>
<script src="//code.jquery.com/jquery-1.11.0.min.js"></script>
<script src="//maxcdn.bootstrapcdn.com/bootstrap/3.3.0/js/bootstrap.min.js"></script>
<script src="/js/highlight.pack.js" type="text/javascript"></script>
<script>hljs.initHighlightingOnLoad();</script>


</body>
</html>
